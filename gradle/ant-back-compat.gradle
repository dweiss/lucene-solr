//
// Ant backward compatibility. All of this should be gone at some point.
//

// Exclude inner classes from testing.
allprojects {
  tasks.withType(Test) { task ->
    exclude '**/*$*'
  }
}

// Adapt to custom folder convention.
allprojects {
  plugins.withType(JavaPlugin) {
    sourceSets {
      main.java.srcDirs = ['src/java']
      main.resources.srcDirs = ['src/resources']
      test.java.srcDirs = ['src/test']
      test.resources.srcDirs = ['src/test-files']
    }

    task copyTestResources(type: Copy) {
      from('src/test') {
        exclude '**/*.java'
      }
      into sourceSets.test.java.outputDir
    }
    processTestResources.dependsOn copyTestResources
  }
}

// Stick to previous artifact names (not identical to path/ folders).
configure(subprojects.findAll { it.path.contains(':analysis:') } ) {
  afterEvaluate {
    plugins.withType(JavaPlugin) {
      project.archivesBaseName = project.archivesBaseName.replace("-analysis-", "-analyzers-")
    }
  }
}

// Add a 'resolve' task that copies dependencies to each module's lib/ folder.
allprojects {
  plugins.withType(JavaPlugin) {
    task resolve(type: Sync) {
      from(configurations.runtimeClasspath, {
        exclude "lucene-*"
      })

      into 'lib'
    }
  }
}
