import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
  id "base"
  id "com.palantir.consistent-versions" version "1.12.4"
  id "com.gradle.build-scan" version "3.0"
}

allprojects {
  tasks.withType(Test) { task ->
    maxParallelForks = (int) Math.max(Runtime.runtime.availableProcessors() / 2.0, 4.0)
  }
}

apply from: file('gradle/buildscan.gradle')
apply from: file('gradle/travis.gradle')

apply from: file('gradle/test-per-project-summary.gradle')
apply from: file('gradle/test-slowest.gradle')

apply from: file('gradle/ant-back-compat.gradle')

allprojects {
  apply plugin: 'base'

  repositories {
    mavenCentral()
  }
}

allprojects {
  plugins.withType(JavaPlugin) {
    sourceCompatibility = "11"
    targetCompatibility = "11"

    compileJava.options.encoding = "UTF-8"
    compileTestJava.options.encoding = "UTF-8"

    project.archivesBaseName = project.path.replaceAll("^:", "").replace(':', '-')
  }
}

allprojects {
  apply plugin: 'idea'

  idea {
    module {
      outputDir file('build/idea/classes/main')
      testOutputDir file('build/idea/classes/test')
      downloadSources = true
    }
  }
}
