
apply plugin: 'java-library'

configurations {
  startJar
  libExt

  api {
    exclude group: "org.slf4j"
  }
}

dependencies {
  startJar('org.eclipse.jetty:jetty-start::shaded', {
    transitive false
  })

  libExt 'com.lmax:disruptor'
  libExt 'org.slf4j:jcl-over-slf4j'
  libExt 'org.slf4j:jul-to-slf4j'
  libExt 'org.slf4j:slf4j-api'
  libExt 'org.apache.logging.log4j:log4j-1.2-api'
  libExt 'org.apache.logging.log4j:log4j-api'
  libExt 'org.apache.logging.log4j:log4j-core'
  libExt 'org.apache.logging.log4j:log4j-slf4j-impl'
  libExt 'org.apache.logging.log4j:log4j-web'

  api('org.eclipse.jetty:jetty-alpn-java-server', {
    exclude group: "org.eclipse.jetty.alpn", module: "alpn-api"
  })

  api 'io.dropwizard.metrics:metrics-core'
  api('io.dropwizard.metrics:metrics-graphite', {
    exclude group: "com.rabbitmq", module: "amqp-client"
  })
  api 'io.dropwizard.metrics:metrics-jetty9'
  api 'io.dropwizard.metrics:metrics-jvm'
  api 'io.dropwizard.metrics:metrics-jmx'

  api 'org.eclipse.jetty:jetty-continuation'
  api 'org.eclipse.jetty:jetty-deploy'
  api 'org.eclipse.jetty:jetty-http'
  api 'org.eclipse.jetty:jetty-io'
  api 'org.eclipse.jetty:jetty-jmx'
  api 'org.eclipse.jetty:jetty-rewrite'
  api 'org.eclipse.jetty:jetty-security'
  api 'org.eclipse.jetty:jetty-server'
  api 'org.eclipse.jetty:jetty-servlet'
  api 'org.eclipse.jetty:jetty-servlets'
  api 'org.eclipse.jetty:jetty-util'
  api 'org.eclipse.jetty:jetty-webapp'
  api 'org.eclipse.jetty:jetty-xml'
  api 'org.eclipse.jetty:jetty-alpn-server'

  api 'org.eclipse.jetty.http2:http2-server'
  api 'org.eclipse.jetty.http2:http2-common'
  api 'org.eclipse.jetty.http2:http2-hpack'

  api 'javax.servlet:javax.servlet-api'
}

task collectDependencies(type: Sync) {
  from { project.configurations.startJar.singleFile } {
    rename { file -> 'start.jar' }\
  }

  from(project.configurations.compileClasspath, {
    into 'lib/'
  })

  from(project.configurations.libExt, {
    into 'lib/ext/'
  })

  from(projectDir, {
    include "contexts/**"
    include "etc/**"
    include "modules/**"
    include "resources/**"
    include "scripts/**"
    include "solr/**"
    include "README.txt"
  })

  into "$buildDir/assembled"
}

// faux module, does not produce any actual JAR files. We only export deps.
jar.enabled = false

// This copies JARs back from the assembly location into the project source location(s).
// TODO: we should get rid of this, it's insane (ant-compat).
task copyIntoProjectSources(type: Copy) {
  dependsOn collectDependencies

  from "$buildDir/assembled"
  into projectDir
}

assemble.dependsOn collectDependencies, copyIntoProjectSources

resolve {
  dependsOn assemble
  preserve "lib/ext"
}
